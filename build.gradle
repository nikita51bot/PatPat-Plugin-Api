plugins {
	id 'java'
	id 'com.vanniktech.maven.publish' version '0.35.0'
}

group = 'ru.nik51'
version = prop('patpat-plugin-api')

base {
	archivesName = 'patpat-plugin-api'
}

def buildJavaString = prop('java-version')
def buildJava = JavaVersion.toVersion(buildJavaString)

repositories {
	mavenCentral()
	maven {
		name = 'spigotmc-repo'
		url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}
}

java {
	sourceCompatibility = buildJava
	targetCompatibility = buildJava
	if (JavaVersion.current() < buildJava) {
		toolchain.languageVersion = JavaLanguageVersion.of(buildJavaString)
	}
}
tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release = buildJavaString as int
}

dependencies {
	// Spigot API
	compileOnly("org.spigotmc:spigot-api:${prop('spigot-api')}")

	// Jetbrains Annotations
	compileOnly("org.jetbrains:annotations:${prop('jetbrains-annotations')}")
}


tasks.withType(Javadoc).configureEach {
	options.docTitle = 'PatPat Plugin API '+version
	options.windowTitle = options.docTitle
}


mavenPublishing {
	coordinates(group.toString(), 'patpat-plugin-api', version.toString())

	pom {
		name = 'PatPat Plugin Api'
		description = 'Public API for PatPat Plugin.'
		inceptionYear = '2026'
		url = 'https://github.com/nikita51bot/PatPat-Plugin-Api'
		licenses {
			license {
				name = 'GNU Lesser General Public License v3.0'
				url = 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
				distribution = 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
			}
		}
		developers {
			developer {
				id = 'nikita51bot'
				name = 'Nikita'
				url = 'https://github.com/nikita51bot/'
			}
		}
		scm {
			url = 'https://github.com/nikita51bot/PatPat-Plugin-Api'
			connection = 'scm:git:git://github.com/nikita51bot/PatPat-Plugin-Api.git'
			developerConnection = 'scm:git:ssh://git@github.com/nikita51bot/PatPat-Plugin-Api.git'
		}
	}
}

afterEvaluate {
	tasks.named('generateMetadataFileForMavenPublication') {
		dependsOn tasks.named('plainJavadocJar')
	}
}

String prop(String key) {
	return findProperty(key).toString()
}